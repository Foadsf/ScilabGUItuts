//*********************************************/
//*  Mise en place de l'interface             */
//*********************************************/
Hauteur=480;
Largeur=640;
// la figure
G = figure('position', [0 0 Largeur Hauteur],...
'Tag','figure_handle',...
'backgroundcolor', name2rgb('lightblue')/255,...
"figure_name", 'Scilab Demo GUI');
//A : Axes pour contenir le graphe
A=newaxes();
 A.auto_clear="on";
 // effacer automatiquement

 // F va contenir les uicontrols
 F = uicontrol(G,...
 'Tag','frame_handle',...
 'position', [0 0 Largeur-2 35], ...
 'fontsize', 12, ...
 'style', 'frame',...
 'string', 'boutons', ...
 'backgroundcolor',  name2rgb('lightgray')/255)


//le curseur
S = uicontrol('parent',F,...
'style', 'slider', ...
'Tag','slider_value',...
'position', [20 2 350 30],....
"Min",0,"Max",10,...
"value",0);
// plusieurs zones de texte
T = uicontrol('parent',F,...
'style', 'text', ...
'Tag','slider_value_display',...
'position', [500 2  80 30],...
'String',' ',...
'callback','disp_callback', ...// va afficher la valeur du curseur
'backgroundcolor',  name2rgb('lightgray')/255,...
'HorizontalAlignment', 'left', ...
'FontAngle', 'italic', ...
'FontSize', 20, ...
'FontWeight', 'bold')


T1 = uicontrol('parent',F,...
'style', 'text', ...
'Tag','slider_value_min',...
'position', [2 2  18 30],...
'String', '0',...
'backgroundcolor',  name2rgb('lightgray')/255,..
'HorizontalAlignment', 'left', ...
'FontAngle', 'italic', ...
'FontSize', 20, ...
'FontWeight', 'bold');

T2 = uicontrol('parent',F,...
'style', 'text', ...
'Tag','slider_value_max',...
'position', [372 2  40 30],...
'String', '10',...
'HorizontalAlignment', 'left', ...
'FontAngle', 'italic', ...
'FontSize', 20, ...
'FontWeight', 'bold');

T3 = uicontrol('parent',F,...
'style', 'text', ...
'Tag','var_name',...
'position', [440 2  40 20],...
'String', '$$a=$$',...
'backgroundcolor',  name2rgb('lightgray')/255,...
'HorizontalAlignment', 'right', ...
'FontSize',20);


// un bouton pour quitter l'interface
B = uicontrol('parent',F,...
'position', [Largeur-55,2 50 30], ...
'Tag','quit_bouton',...
'fontsize', 12, ...
'style', 'pushbutton',...
'string', 'Quitter', ...
'callback', 'quit_callback', ...// va stopper la boucle et fermer la fenêtre
'backgroundcolor', name2rgb('red')/255);

//*********************************************
//  les callbacks
//*********************************************
function disp_callback()
   //récupère la valeur du curseur
   E1=findobj("Tag","slider_value");
   value=E1.value;
   //change la valeur affichée
   E2=findobj("Tag","slider_value_display");
   E2.string=string(value)
endfunction


function plot_callback()
   //récupère la valeur du curseur
   E1=findobj("Tag","slider_value");
   value=E1.value;
   //trace la courbe
   drawlater()
   x=[1:0.001:20*%pi];

   y=cos(x).*cos(value*x)
   plot(x,y,'-r')
   xgrid(3)
   A=gca()
    A.title.text="$$y=\cos(x)\cos(a x)$$";
    A.title.font_size=4;
    drawnow()
endfunction

function quit_callback()
   h=findobj("Tag","figure_handle");
   // récupère le handle de la fenêtre
   delete(h);
   abort  //  quitte la boucle while
endfunction


//*********************************************
//  lancement de l'interface
//*********************************************
cont=%t
while cont // la boucle principale
   if findobj("Tag","figure_handle")==[] then
      cont=%f; // on quitte la fenêtre prématurément
      break
   else
      F.position=[0 0 G.figure_size(1)-2 35];
      disp_callback()  // mise à jour de la valeur a
      plot_callback()  // mise à jour du graphe
   end
end
delete(gcf())  // si on quitte la fenêtre sans utiliser le boutton
